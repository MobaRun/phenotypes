import os
import json

##
#
# This script processes the raw phenotypes of MoBa into text tables and writes documentation.
#
# Commands to execute from the repository folder:
# conda activate snakemake
# snakemake --snakefile src/run_phenotypes.snake --cores 2 --use-conda --rerun-incomplete
##

####################### Parameters ###########################################

# Analysis
suffix = '22-09-19'
moba_project_number = 2824

# Raw phenotype files
linkage_folder = "/mnt/archive/linkage"
moba_ques_folder = "/mnt/archive/phenotype/moba_ques"
msis_folder = "/mnt/archive/phenotype/msis"
sysvak_folder = "/mnt/archive/phenotype/sysvak"
ques_folder = "/mnt/archive/phenotype/moba_covid_ques"

# Docs folders
docs_folder = f"docs/{suffix}"
raw_docs_folder = f"{docs_folder}/raw"
covid_docs_folder = f"{docs_folder}/covid"

# Tables folders
tables_folder = f"/mnt/work/marc/pheno_{suffix}"
raw_tables_folder = f"/mnt/work/marc/pheno_{suffix}/raw"
covid_tables_folder = f"/mnt/work/marc/pheno_{suffix}/covid"

# Raw tables
mfr_raw_table = os.path.join(raw_tables_folder, 'PDB2824_MFR_541_v12.gz')
msis_raw_table = os.path.join(raw_tables_folder, f'PDB{moba_project_number}_MSIS-data_MoBa.gz')
child_id_mapping_raw_table = os.path.join(raw_tables_folder, f'Barn_ID_{moba_project_number}_2021_11_17sav.gz')
mother_id_mapping_raw_table = os.path.join(raw_tables_folder, f'Mor_ID_{moba_project_number}_2021_11_17sav.gz')
father_id_mapping_raw_table = os.path.join(raw_tables_folder, f'Far_ID_{moba_project_number}_2021_11_17sav.gz')
child_id_linkage_raw_table = os.path.join(raw_tables_folder, f'20220516_MoBaGeneticsTot_Child_PDB{moba_project_number}.sav.gz')
mother_id_linkage_raw_table = os.path.join(raw_tables_folder, f'20220516_MoBaGeneticsTot_Mother_PDB{moba_project_number}.sav.gz')
father_id_linkage_raw_table = os.path.join(raw_tables_folder, f'20220516_MoBaGeneticsTot_Father_PDB{moba_project_number}.sav.gz')


# Covid table
covid_table = os.path.join(covid_tables_folder, "moba_covid_phenotypes.gz")


####################### Housekeeping #########################################

if not os.path.isdir(docs_folder):
    os.mkdir(docs_folder)

if not os.path.isdir(raw_docs_folder):
    os.mkdir(raw_docs_folder)

if not os.path.isdir(covid_docs_folder):
    os.mkdir(covid_docs_folder)

if not os.path.isdir(tables_folder):
    os.mkdir(tables_folder)

if not os.path.isdir(raw_tables_folder):
    os.mkdir(raw_tables_folder)

if not os.path.isdir(covid_tables_folder):
    os.mkdir(covid_tables_folder)


####################### Functions ############################################




############################## RULES #########################################

rule all:
    'The output of the pipeline'
    input:
        os.path.join(raw_docs_folder,'data.md'), # The raw data processing
        covid_table, # The covid table

rule raw_pheno:
    'Convert raw phenotypes and write associated documentation'
    input:
        linkage_folder = linkage_folder,
        moba_ques_folder = moba_ques_folder,
        msis_folder = msis_folder,
        sysvak_folder = sysvak_folder,
        ques_folder = ques_folder
    params:
        moba_project_number = moba_project_number,
        raw_tables_folder = raw_tables_folder,
        raw_docs_folder = raw_docs_folder
    output:
        docs_file = os.path.join(raw_docs_folder, 'data.md'),
        mfr_raw_table = mfr_raw_table,
        msis_raw_table = msis_raw_table,
        child_id_mapping_raw_table = child_id_mapping_raw_table,
        mother_id_mapping_raw_table = mother_id_mapping_raw_table,
        father_id_mapping_raw_table = father_id_mapping_raw_table,
        child_id_linkage_raw_table = child_id_linkage_raw_table,
        mother_id_linkage_raw_table = mother_id_linkage_raw_table,
        father_id_linkage_raw_table = father_id_linkage_raw_table
    conda:
        "envs/r_4.1.yaml"
    threads: 1
    shell:
        """
        Rscript src/scripts/raw_pheno_docs.R \
            {input.linkage_folder} \
            {input.moba_ques_folder} \
            {input.msis_folder} \
            {input.sysvak_folder} \
            {input.ques_folder} \
            {params.raw_tables_folder} \
            {params.raw_docs_folder} \
            {params.moba_project_number}
        """

rule consolidate_covid_pheno:
    'Consolidates the covid questionnaires into a single table'
    input:
        mfr_raw_table = mfr_raw_table,
        msis_raw_table = msis_raw_table,
        child_id_mapping_raw_table = child_id_mapping_raw_table,
        mother_id_mapping_raw_table = mother_id_mapping_raw_table,
        father_id_mapping_raw_table = father_id_mapping_raw_table,
        child_id_linkage_raw_table = child_id_linkage_raw_table,
        mother_id_linkage_raw_table = mother_id_linkage_raw_table,
        father_id_linkage_raw_table = father_id_linkage_raw_table
    params:
        linkage_folder = linkage_folder,
        ques_folder = ques_folder,
        tables_folder = tables_folder,
        covid_docs_folder = moba_project_number,
        raw_tables_folder = raw_tables_folder,
        raw_docs_folder = raw_docs_folder
    output:
        covid_table = covid_table
    conda:
        "envs/r_4.1.yaml"
    threads: 1
    shell:
        """
        Rscript src/scripts/consolidate_covid_phenotypes.R \
            {params.linkage_folder} \
            {params.ques_folder} \
            {params.raw_tables_folder} \
            {output.covid_table} \
            {params.covid_docs_folder}
        """



